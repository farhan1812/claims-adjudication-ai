grammar org.xtext.example.mydsl.MyDsl hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

Feature:
	(tags+=Tag+ EOL)?
	'Feature:' name=Phrase EOL
	statements+=Statement*
	abstractScenarios+=AbstractScenario*;

// This has the side effect of allowing more than 1 Background.
// Also it allows it to be anywhere, like at the begining or end.
// I assume anyone using this editor knows that in Cucumber it just goes once at the top
// I'm doing it this way to make the formatting code simpler by having less duplication
AbstractScenario:
	Background | Scenario | ScenarioOutline;

Background:
	'Background:' name=Phrase EOL
	statements+=Statement*
	steps+=Step*;

Scenario:
	(tags+=Tag+ EOL)?
	'Scenario:' name=Phrase EOL
	statements+=Statement*
	steps+=Step*;

ScenarioOutline:
	(tags+=Tag+ EOL)?
	'Scenario Outline:' name=Phrase EOL
	statements+=Statement*
	steps+=Step*
	examples+=Examples+;

Examples:
	(tags+=Tag+ EOL)?
	'Examples:' name=Phrase EOL
	statements+=Statement*
	theExamplesTable=ExamplesTable;

ExamplesTable:
	rows+=Row+;

StepTable:
	rows+=Row+;
	
Row:
	cells+=Cell+ '|' EOL;

Cell:
	cell='|' name=Phrase;

Step:
	Given | When | Then | And | But | Asterisk;

DocString:
	'"""' EOL 
	statements+=Statement+
	'"""' EOL;

Given:
	'Given' name=Phrase EOL
	(theStepTable=StepTable | theDocString=DocString)?;

When:
	'When' name=Phrase EOL
	(theStepTable=StepTable | theDocString=DocString)?;

Then:
	'Then' name=Phrase EOL
	(theStepTable=StepTable | theDocString=DocString)?;

And:
	'And' name=Phrase EOL
	(theStepTable=StepTable | theDocString=DocString)?;

But:
	'But' name=Phrase EOL
	(theStepTable=StepTable | theDocString=DocString)?;

Asterisk:
	'*' name=Phrase EOL
	(theStepTable=StepTable | theDocString=DocString)?;

Statement:
	name=Phrase EOL;

Phrase:
	ID+;

Tag:
	'@' name=ID;

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_' | '.' | '0'..'9')*;

terminal WS:
	(' ' | '\t')+;

// single line comment
terminal SL_COMMENT:
	'#' !('\r' | '\n')* EOL;

terminal EOL:
	('\r\n')+;
