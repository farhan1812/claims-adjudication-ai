grammar org.farhan.Cucumber hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumber "http://www.farhan.org/Cucumber"

Feature:
	(tag+=Tag+ EOL)?
	'Feature:' title=Sentence EOL
	(descriptions+=Description)*
	(background=Background)?
	(scenarios+=AbstractScenario)*;

Background:
	'Background:' title=Sentence EOL
	description=Description
	(steps+=Step)*;

AbstractScenario:
	ScenarioOutline | Scenario;

Scenario:
	(tag+=Tag EOL)?
	'Scenario:' title=Sentence EOL
	description=Description
	(steps+=Step)*;

ScenarioOutline:
	(tag+=Tag EOL)?
	'Scenario Outline:' title=Sentence EOL
	description=Description
	(steps+=Step)*
	(examples+=Example+);

Example:
	(tag+=Tag EOL)?
	'Examples:' title=Sentence EOL
	description=Description
	table=Table;

Description:
	line=Sentence EOL;

Step:
	('Given' | 'When' | 'Then' | 'And' | 'But' | '*') title=Sentence EOL?
	(table=Table | text=DocString)?;

Table:
	rows+=TableRow+;

TableRow:
	cells+=RowCell+ '|' EOL;

RowCell:
	cell='|' Sentence;

DocString:
	{DocString}
	'"""' (Sentence | EOL)* '"""' EOL;

Tag:
	tag=TAG_ID;

Sentence:
	WORD+;

terminal WS:
	(' ' | '\t')+;

terminal EOL:
	('\r' | '\n')+;

// single line comment
terminal SL_COMMENT:
	'#' !('\r' | '\n')* EOL;

// need to add punctuation and commas etc
// this covers Given When Then and might create problems?
terminal WORD:
	!('@' | '|' | ' ' | '\t' | '\n' | '\r')+;

terminal TAG_ID:
	'@' WORD;