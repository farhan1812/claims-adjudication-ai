grammar org.farhan.Cucumber hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumber "http://www.farhan.org/Cucumber"

Feature:
	(tags+=Tag+ EOL)?
	'Feature:' name=Phrase EOL
	statements+=Statement*
	abstractScenarios+=AbstractScenario*;

// TODO add parameters to step, cell, line tokens
// This has the side effect of allowing more than 1 Background.
// Also it allows it to be anywhere, like at the begining or end.
// I'm doing it this way to make the formatting code simpler by having less duplication
AbstractScenario:
	Background | Scenario | ScenarioOutline;

Background:
	'Background:' name=Phrase EOL
	statements+=Statement*
	steps+=Step*;

Scenario:
	(tags+=Tag+ EOL)?
	'Scenario:' name=Phrase EOL
	statements+=Statement*
	steps+=Step*;

ScenarioOutline:
	(tags+=Tag+ EOL)?
	'Scenario Outline:' name=Phrase EOL
	statements+=Statement*
	steps+=Step*
	examples+=Examples+;

Examples:
	(tags+=Tag+ EOL)?
	'Examples:' name=Phrase EOL
	statements+=Statement*
	theExamplesTable=ExamplesTable;

ExamplesTable:
	rows+=Row+;

StepTable:
	rows+=Row+;

Row:
	cells+=Cell+ '|' EOL;

Cell:
	'|' name=Phrase;

Step:
	('Given' | 'When' | 'Then' | 'And' | '*') name=Phrase EOL
	(theStepTable=StepTable | theDocString=DocString)?;

DocString:
	'"""' EOL
	lines+=Line+
	'"""' EOL;

Statement:
	name=Phrase EOL;

Phrase:
	TEXT_LITERAL+;

Tag:
	'@' name=ID;

Line:
	name=LineBits EOL;

LineBits:
	TEXT_LITERAL*;

TEXT_LITERAL:
	WORD
	| ID
	| STRING;

terminal ID:
	('0'..'9' | 'a'..'z' | 'A'..'Z' | '_' | '-' | '.')+;

terminal WORD:
	!('@' | '|' | ' ' | '\t' | '\n' | '\r')
	!(' ' | '\t' | '\n' | '\r')*;

// Not sure how this is helping, will remove it
terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"' | '\r' | '\n'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'" | '\r' | '\n'))* "'";

terminal WS:
	(' ' | '\t' | '\r')+;

// single line comment
terminal SL_COMMENT:
	'#' !('\n')* EOL;

terminal EOL:
	('\n')+;