grammar org.farhan.cukefsm.CucumberFSM hidden(WS, COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumberFSM "http://www.farhan.org/cukefsm/CucumberFSM"

Feature:
	Tags
	'Feature:' title=Sentence EOL?
	description=Description
	background=Background?
	(scenarios+=AbstractScenario)*;

Background:
	'Background:' title=Sentence EOL?
	description=Description
	(steps+=Step)*;

AbstractScenario:
	ScenarioOutline | Scenario;

Scenario:
	Tags
	'Scenario:' title=Sentence EOL?
	description=Description
	(steps+=Step)*;

ScenarioOutline:
	Tags
	'Scenario Outline:' title=Sentence EOL?
	description=Description
	(steps+=Step)*
	(examples+=Example+);

Example:
	Tags
	'Examples:' title=Sentence EOL?
	description=Description
	table=Table;

Step:
	('Given' | 'When' | 'Then' | 'And' | 'But' | '*') title=Sentence EOL?
	(table=Table | text=DocString)?;

Table:
	rows+=TableRow+;

TableRow:
	cells+=RowCell+ '|' EOL;

RowCell:
	cell='|' Sentence;

DocString:
	{DocString}
	'"""' (Sentence | EOL)* '"""' EOL;

// make this optional
Description:
	{Description} (sentences+=Sentence EOL?)*;

Tags:
	TAG* EOL?;

Sentence:
	WORD+;

terminal WS:
	(' ' | '\t')+;

terminal EOL:
	('\r' | '\n')+;

// single line comment
terminal COMMENT:
	'#' !('\r' | '\n')* EOL;

// need to add punctuation and commas etc
// this covers Given When Then and might create problems?
terminal WORD:
	!('@' | '|' | ' ' | '\t' | '\n' | '\r')+;

terminal TAG:
	'@' WORD;