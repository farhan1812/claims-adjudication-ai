// TODO try using the built in terminals instead and override them
grammar org.farhan.mbt.SheepDog hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate sheepDog "http://www.farhan.org/mbt/SheepDog"

Model:
	StepObject | Feature;

StepObject:
	'=' 'Object:' name=Title
	statements+=Statement*
	stepDefinitions+=StepDefinition*;

StepDefinition:
	'==' 'Definition:' name=Title
	statements+=Statement*
	stepParameters+=StepParameters*;

StepParameters:
	'===' 'Parameters:' name=Title
	statements+=Statement*
	parametersTable=Table;

Feature:
	'=' 'Feature:' name=Title
	statements+=Statement*
	abstractScenarios+=AbstractScenario*;

AbstractScenario:
	Background | Scenario;

Background:
	(tags=Tags)?
	'==' 'Background:' name=Title
	statements+=Statement*
	steps+=Step*;

Scenario:
	(tags=Tags)?
	'==' 'Scenario:' name=Title
	statements+=Statement*
	steps+=Step*
	examples+=Examples*;

Examples:
	(tags=Tags)?
	'===' 'Examples:' name=Title
	statements+=Statement*
	theExamplesTable=Table;

Step:
	Given | When | Then | And;

Given:
	'===' 'Given' name=Title
	(theStepTable=Table | theDocString=DocString)?;

When:
	'===' 'When' name=Title
	(theStepTable=Table | theDocString=DocString)?;

Then:
	'===' 'Then' name=Title
	(theStepTable=Table | theDocString=DocString)?;

And:
	'===' 'And' name=Title
	(theStepTable=Table | theDocString=DocString)?;

DocString:
	'----' EOL
	lines+=Line+
	'----' EOL;

Table:
	'[options="header"]' EOL
	'|===' EOL
	rows+=Row+
	'|===' EOL;

Row:
	cells+=Cell+ EOL;

Tags:
	'[tags="' TEXT_LITERAL+ '"]' EOL;

Title:
	TEXT_LITERAL+ EOL;

Statement:
	TEXT_LITERAL+ EOL;

Line:
	TEXT_LITERAL* EOL;

Cell:
	'|' TEXT_LITERAL*;

TEXT_LITERAL:
	WORD
	| ID
	| STRING;

terminal ID:
	('0'..'9' | 'a'..'z' | 'A'..'Z' | '_' | '-' | '.' | ',')+;

terminal WORD:
	!('|' | ' ' | '\t' | '\n' | '\r') !(' ' | '\t' | '\n' | '\r')*;

// Not sure how this is helping, will remove it
terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"' | '\r' | '\n'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'" | '\r' | '\n'))* "'";

terminal WS:
	(' ' | '\t' | '\r')+;

// single line comment
terminal SL_COMMENT:
	'#' !('\n')* EOL;

terminal EOL:
	('\n')+;