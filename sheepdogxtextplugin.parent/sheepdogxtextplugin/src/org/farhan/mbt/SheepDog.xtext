grammar org.farhan.mbt.SheepDog hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate sheepDog "http://www.farhan.org/mbt/SheepDog"

Model:
	StepObject | Feature;

Feature:
	tags=FeatureTags?
	'=' name=Phrase EOL
	statements+=Statement*
	background=Background?
	scenarios+=Scenario*;

Background:
	'[background="true"]' EOL
	'==' name=Phrase EOL
	statements+=Statement*
	steps+=Step*;

Scenario:
	tags=ScenarioTags?
	'==' name=Phrase EOL
	statements+=Statement*
	steps+=Step*
	examples+=Examples+;

Examples:
	'[examples="true"]' EOL
	'===' name=Phrase EOL
	statements+=Statement*
	theExamplesTable=Table;

ScenarioTags:
	'[tags="' name=Phrase '"]' EOL;

FeatureTags:
	':tags:' name=Phrase EOL;

Step:
	Given | When | Then;

Given:
	'===' 'Given' name=Phrase EOL
	(theStepTable=Table | theDocString=DocString)?;

When:
	'===' 'When' name=Phrase EOL
	(theStepTable=Table | theDocString=DocString)?;

Then:
	'===' 'Then' name=Phrase EOL
	(theStepTable=Table | theDocString=DocString)?;

DocString:
	'----' EOL
	lines+=Line+
	'----' EOL;

Line:
	name=LineBits EOL;

LineBits:
	TEXT_LITERAL*;

// TODO this is duplicate of StepTable
Table:
	'[options="header"]' EOL
	'|===' EOL
	rows+=Row+
	'|===' EOL;

Row:
	cells+=Cell+ EOL;

Cell:
	'|' name=Phrase;

Statement:
	name=Phrase EOL;

Phrase:
	TEXT_LITERAL+;

TEXT_LITERAL:
	WORD
	| ID
	| STRING;

StepObject:
	'=' name=Phrase EOL
	statements+=Statement*
	stepDefinitions+=StepDefinition*;

StepDefinition:
	'==' name=Phrase EOL
	statements+=Statement*
	stepParameters+=StepParameters*;

StepParameters:
	'===' name=Phrase EOL
	statements+=Statement*
	parametersTable=Table;

terminal ID:
	('0'..'9' | 'a'..'z' | 'A'..'Z' | '_' | '-' | '.' | ',')+;

terminal WORD:
	!('@' | '|' | ' ' | '\t' | '\n' | '\r')
	!(' ' | '\t' | '\n' | '\r')*;

// Not sure how this is helping, will remove it
terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"' | '\r' | '\n'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'" | '\r' | '\n'))* "'";

terminal WS:
	(' ' | '\t' | '\r')+;

// single line comment
terminal SL_COMMENT:
	'#' !('\n')* EOL;

terminal EOL:
	('\n')+;